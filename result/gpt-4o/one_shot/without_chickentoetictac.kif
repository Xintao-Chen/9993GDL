(role xplayer)
(role oplayer)

(location a)
(location b)
(location c)
(location d)
(location e)
(location f)
(location g)
(location h)
(location i)

(init (control xplayer))

(<= (legal xplayer noop)
    (true (control oplayer)))
(<= (legal xplayer (mark ?loc))
    (true (control xplayer))
    (blank ?loc))
(<= (legal oplayer noop)
    (true (control xplayer)))
(<= (legal oplayer (mark ?loc))
    (true (control oplayer))
    (blank ?loc))

(<= (next (marked ?loc ?player))
    (role ?player)
    (does ?player (mark ?loc)))
(<= (next (marked ?loc ?player))
    (true (marked ?loc ?player)))
(<= (next (control xplayer))
    (true (control oplayer)))
(<= (next (control oplayer))
    (true (control xplayer)))

(<= terminal
    (line xplayer))
(<= terminal
    (line oplayer))
(<= terminal
    (not open))

(<= (goal xplayer 100)
    (line xplayer))
(<= (goal xplayer 50)
    (not (line xplayer))
    (not (line oplayer))
    (not open))
(<= (goal xplayer 0)
    (line oplayer))
(<= (goal xplayer 0)
    (not (line xplayer))
    (not (line oplayer))
    open)
(<= (goal oplayer 100)
    (line oplayer))
(<= (goal oplayer 50)
    (not (line xplayer))
    (not (line oplayer))
    (not open))
(<= (goal oplayer 0)
    (line xplayer))
(<= (goal oplayer 0)
    (not (line xplayer))
    (not (line oplayer))
    open)

(<= (row ?mark)
    (true (marked a ?mark))
    (true (marked b ?mark))
    (true (marked c ?mark)))
(<= (row ?mark)
    (true (marked d ?mark))
    (true (marked e ?mark))
    (true (marked f ?mark)))
(<= (row ?mark)
    (true (marked g ?mark))
    (true (marked h ?mark))
    (true (marked i ?mark)))

(<= (col ?mark)
    (true (marked a ?mark))
    (true (marked d ?mark))
    (true (marked g ?mark)))
(<= (col ?mark)
    (true (marked b ?mark))
    (true (marked e ?mark))
    (true (marked h ?mark)))
(<= (col ?mark)
    (true (marked c ?mark))
    (true (marked f ?mark))
    (true (marked i ?mark)))

(<= (diag ?mark)
    (true (marked a ?mark))
    (true (marked e ?mark))
    (true (marked i ?mark)))
(<= (diag ?mark)
    (true (marked c ?mark))
    (true (marked e ?mark))
    (true (marked g ?mark)))

(<= (line ?mark)
    (row ?mark))
(<= (line ?mark)
    (col ?mark))
(<= (line ?mark)
    (diag ?mark))

(<= (blank ?loc)
    (location ?loc)
    (not (true (marked ?loc xplayer)))
    (not (true (marked ?loc oplayer))))

(<= open
    (blank ?loc))