(role xplayer)
(role oplayer)

(position a)
(position b)
(position c)
(position d)
(position e)
(position f)
(position g)
(position h)
(position i)

(init (control xplayer))

(<= (legal xplayer noop)
    (true (control oplayer)))
(<= (legal xplayer (mark ?pos x))
    (true (control xplayer))
    (blank ?pos))
(<= (legal oplayer noop)
    (true (control xplayer)))
(<= (legal oplayer (mark ?pos o))
    (true (control oplayer))
    (blank ?pos))

(<= (next (marked ?pos ?mark))
    (role ?player)
    (does ?player (mark ?pos ?mark)))
(<= (next (marked ?pos ?mark))
    (true (marked ?pos ?mark)))
(<= (next (control xplayer))
    (true (control oplayer)))
(<= (next (control oplayer))
    (true (control xplayer)))

(<= terminal
    (line x))
(<= terminal
    (line o))
(<= terminal
    (not open))

(<= (goal xplayer 100)
    (line x))
(<= (goal xplayer 50)
    (not (line x))
    (not (line o))
    (not open))
(<= (goal xplayer 0)
    (line o))
(<= (goal xplayer 0)
    (not (line x))
    (not (line o))
    open)
(<= (goal oplayer 100)
    (line o))
(<= (goal oplayer 50)
    (not (line x))
    (not (line o))
    (not open))
(<= (goal oplayer 0)
    (line x))
(<= (goal oplayer 0)
    (not (line x))
    (not (line o))
    open)

(<= (row ?mark)
    (true (marked a ?mark))
    (true (marked b ?mark))
    (true (marked c ?mark)))
(<= (row ?mark)
    (true (marked d ?mark))
    (true (marked e ?mark))
    (true (marked f ?mark)))
(<= (row ?mark)
    (true (marked g ?mark))
    (true (marked h ?mark))
    (true (marked i ?mark)))
(<= (col ?mark)
    (true (marked a ?mark))
    (true (marked d ?mark))
    (true (marked g ?mark)))
(<= (col ?mark)
    (true (marked b ?mark))
    (true (marked e ?mark))
    (true (marked h ?mark)))
(<= (col ?mark)
    (true (marked c ?mark))
    (true (marked f ?mark))
    (true (marked i ?mark)))
(<= (diag ?mark)
    (true (marked a ?mark))
    (true (marked e ?mark))
    (true (marked i ?mark)))
(<= (diag ?mark)
    (true (marked c ?mark))
    (true (marked e ?mark))
    (true (marked g ?mark)))

(<= (line ?mark)
    (row ?mark))
(<= (line ?mark)
    (col ?mark))
(<= (line ?mark)
    (diag ?mark))

(<= (blank ?pos)
    (position ?pos)
    (not (true (marked ?pos x)))
    (not (true (marked ?pos o))))
(<= open
    (blank ?pos))