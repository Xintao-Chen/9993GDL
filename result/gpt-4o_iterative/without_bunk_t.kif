(role player1)
(role player2)

(index 1)
(index 2)
(index 3)

(init (control player1))
(init (cell 1 1 both))
(init (cell 1 2 both))
(init (cell 1 3 both))
(init (cell 2 1 both))
(init (cell 2 2 both))
(init (cell 2 3 both))
(init (cell 3 1 both))
(init (cell 3 2 both))
(init (cell 3 3 both))

(<= (legal player1 noop)
    (true (control player2)))
(<= (legal player1 (place ?i ?j white))
    (true (control player1))
    (emptyCell ?i ?j))
(<= (legal player2 noop)
    (true (control player1)))
(<= (legal player2 (place ?i ?j black))
    (true (control player2))
    (emptyCell ?i ?j))

(<= (next (cell ?i ?j white))
    (does player1 (place ?i ?j white)))
(<= (next (cell ?i ?j black))
    (does player2 (place ?i ?j black)))
(<= (next (cell ?i ?j ?color))
    (true (cell ?i ?j ?color))
    (not (does player1 (place ?i ?j white)))
    (not (does player2 (place ?i ?j black))))

(<= (next (control player1))
    (true (control player2)))
(<= (next (control player2))
    (true (control player1)))

(<= terminal
    (line white))
(<= terminal
    (line black))
(<= terminal
    (not open))

(<= (goal player1 100)
    (line white)
    (line black))
(<= (goal player1 75)
    (line white)
    (not (line black)))
(<= (goal player1 25)
    (line black)
    (not (line white)))
(<= (goal player1 50)
    (not (line white))
    (not (line black))
    (not open))

(<= (goal player2 100)
    (line white)
    (line black))
(<= (goal player2 75)
    (line black)
    (not (line white)))
(<= (goal player2 25)
    (line white)
    (not (line black)))
(<= (goal player2 50)
    (not (line white))
    (not (line black))
    (not open))

(<= (row ?i ?color)
    (true (cell ?i 1 ?color))
    (true (cell ?i 2 ?color))
    (true (cell ?i 3 ?color)))
(<= (col ?j ?color)
    (true (cell 1 ?j ?color))
    (true (cell 2 ?j ?color))
    (true (cell 3 ?j ?color)))
(<= (diag ?color)
    (true (cell 1 1 ?color))
    (true (cell 2 2 ?color))
    (true (cell 3 3 ?color)))
(<= (diag ?color)
    (true (cell 1 3 ?color))
    (true (cell 2 2 ?color))
    (true (cell 3 1 ?color)))

(<= (line ?color)
    (index ?i)
    (row ?i ?color))
(<= (line ?color)
    (index ?j)
    (col ?j ?color))
(<= (line ?color)
    (diag ?color))

(<= (emptyCell ?i ?j)
    (index ?i)
    (index ?j)
    (true (cell ?i ?j both))
    (not (line white))
    (not (line black)))

(<= open
    (emptyCell ?i ?j))