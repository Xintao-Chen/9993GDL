(role xplayer)
(role oplayer)

(index 1)
(index 2)
(index 3)
(index 4)

(init (control xplayer))

(<= (legal xplayer noop)
    (true (control oplayer)))
(<= (legal xplayer (play ?x ?y ?z x))
    (true (control xplayer))
    (emptyCell ?x ?y ?z))
(<= (legal oplayer noop)
    (true (control xplayer)))
(<= (legal oplayer (play ?x ?y ?z o))
    (true (control oplayer))
    (emptyCell ?x ?y ?z))

(<= (next (mark ?x ?y ?z ?mark))
    (role ?player)
    (does ?player (play ?x ?y ?z ?mark)))
(<= (next (mark ?x ?y ?z ?mark))
    (true (mark ?x ?y ?z ?mark)))
(<= (next (control xplayer))
    (true (control oplayer)))
(<= (next (control oplayer))
    (true (control xplayer)))

(<= terminal
    (line x))
(<= terminal
    (line o))
(<= terminal
    (not open))

(<= (goal xplayer 100)
    (line x))
(<= (goal xplayer 50)
    (not (line x))
    (not (line o))
    (not open))
(<= (goal xplayer 0)
    (line o))
(<= (goal xplayer 0)
    (not (line x))
    (not (line o))
    open)
(<= (goal oplayer 100)
    (line o))
(<= (goal oplayer 50)
    (not (line x))
    (not (line o))
    (not open))
(<= (goal oplayer 0)
    (line x))
(<= (goal oplayer 0)
    (not (line x))
    (not (line o))
    open)

(<= (row ?x ?y ?mark)
    (true (mark ?x ?y 1 ?mark))
    (true (mark ?x ?y 2 ?mark))
    (true (mark ?x ?y 3 ?mark))
    (true (mark ?x ?y 4 ?mark)))
(<= (col ?x ?z ?mark)
    (true (mark ?x 1 ?z ?mark))
    (true (mark ?x 2 ?z ?mark))
    (true (mark ?x 3 ?z ?mark))
    (true (mark ?x 4 ?z ?mark)))
(<= (depth ?y ?z ?mark)
    (true (mark 1 ?y ?z ?mark))
    (true (mark 2 ?y ?z ?mark))
    (true (mark 3 ?y ?z ?mark))
    (true (mark 4 ?y ?z ?mark)))
(<= (diagXY ?z ?mark)
    (true (mark 1 1 ?z ?mark))
    (true (mark 2 2 ?z ?mark))
    (true (mark 3 3 ?z ?mark))
    (true (mark 4 4 ?z ?mark)))
(<= (diagXY ?z ?mark)
    (true (mark 1 4 ?z ?mark))
    (true (mark 2 3 ?z ?mark))
    (true (mark 3 2 ?z ?mark))
    (true (mark 4 1 ?z ?mark)))
(<= (diagXZ ?y ?mark)
    (true (mark 1 ?y 1 ?mark))
    (true (mark 2 ?y 2 ?mark))
    (true (mark 3 ?y 3 ?mark))
    (true (mark 4 ?y 4 ?mark)))
(<= (diagXZ ?y ?mark)
    (true (mark 1 ?y 4 ?mark))
    (true (mark 2 ?y 3 ?mark))
    (true (mark 3 ?y 2 ?mark))
    (true (mark 4 ?y 1 ?mark)))
(<= (diagYZ ?x ?mark)
    (true (mark ?x 1 1 ?mark))
    (true (mark ?x 2 2 ?mark))
    (true (mark ?x 3 3 ?mark))
    (true (mark ?x 4 4 ?mark)))
(<= (diagYZ ?x ?mark)
    (true (mark ?x 1 4 ?mark))
    (true (mark ?x 2 3 ?mark))
    (true (mark ?x 3 2 ?mark))
    (true (mark ?x 4 1 ?mark)))
(<= (diagXYZ ?mark)
    (true (mark 1 1 1 ?mark))
    (true (mark 2 2 2 ?mark))
    (true (mark 3 3 3 ?mark))
    (true (mark 4 4 4 ?mark)))
(<= (diagXYZ ?mark)
    (true (mark 1 1 4 ?mark))
    (true (mark 2 2 3 ?mark))
    (true (mark 3 3 2 ?mark))
    (true (mark 4 4 1 ?mark)))
(<= (diagXYZ ?mark)
    (true (mark 1 4 1 ?mark))
    (true (mark 2 3 2 ?mark))
    (true (mark 3 2 3 ?mark))
    (true (mark 4 1 4 ?mark)))
(<= (diagXYZ ?mark)
    (true (mark 1 4 4 ?mark))
    (true (mark 2 3 3 ?mark))
    (true (mark 3 2 2 ?mark))
    (true (mark 4 1 1 ?mark)))

(<= (line ?mark)
    (index ?x)
    (index ?y)
    (row ?x ?y ?mark))
(<= (line ?mark)
    (index ?x)
    (index ?z)
    (col ?x ?z ?mark))
(<= (line ?mark)
    (index ?y)
    (index ?z)
    (depth ?y ?z ?mark))
(<= (line ?mark)
    (index ?z)
    (diagXY ?z ?mark))
(<= (line ?mark)
    (index ?y)
    (diagXZ ?y ?mark))
(<= (line ?mark)
    (index ?x)
    (diagYZ ?x ?mark))
(<= (line ?mark)
    (diagXYZ ?mark))

(<= (emptyCell ?x ?y ?z)
    (index ?x)
    (index ?y)
    (index ?z)
    (not (true (mark ?x ?y ?z x)))
    (not (true (mark ?x ?y ?z o))))
(<= open
    (emptyCell ?x ?y ?z))