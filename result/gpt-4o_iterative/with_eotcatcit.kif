(role white)
(role black)

(index 1)
(index 2)
(index 3)

(init (control white))
(init (cell 1 1 b))
(init (cell 1 2 b))
(init (cell 1 3 b))
(init (cell 2 1 b))
(init (cell 2 2 b))
(init (cell 2 3 b))
(init (cell 3 1 b))
(init (cell 3 2 b))
(init (cell 3 3 b))

(<= (legal white noop)
    (true (control black)))
(<= (legal white (mark ?i ?j x))
    (true (control white))
    (emptyCell ?i ?j))
(<= (legal black noop)
    (true (control white)))
(<= (legal black (mark ?i ?j o))
    (true (control black))
    (emptyCell ?i ?j))

(<= (next (cell ?i ?j x))
    (does white (mark ?i ?j x)))
(<= (next (cell ?i ?j o))
    (does black (mark ?i ?j o)))
(<= (next (cell ?i ?j ?mark))
    (true (cell ?i ?j ?mark))
    (not (does white (mark ?i ?j x)))
    (not (does black (mark ?i ?j o))))
(<= (next (control white))
    (true (control black)))
(<= (next (control black))
    (true (control white)))

(<= terminal
    (line x))
(<= terminal
    (line o))
(<= terminal
    (not open))

(<= (goal white 100)
    (line o))
(<= (goal white 50)
    (not (line x))
    (not (line o))
    (not open))
(<= (goal white 0)
    (line x))
(<= (goal white 0)
    (not (line x))
    (not (line o))
    open)
(<= (goal black 100)
    (line x))
(<= (goal black 50)
    (not (line x))
    (not (line o))
    (not open))
(<= (goal black 0)
    (line o))
(<= (goal black 0)
    (not (line x))
    (not (line o))
    open)

(<= (row ?i ?mark)
    (true (cell ?i 1 ?mark))
    (true (cell ?i 2 ?mark))
    (true (cell ?i 3 ?mark)))
(<= (col ?j ?mark)
    (true (cell 1 ?j ?mark))
    (true (cell 2 ?j ?mark))
    (true (cell 3 ?j ?mark)))
(<= (diag ?mark)
    (true (cell 1 1 ?mark))
    (true (cell 2 2 ?mark))
    (true (cell 3 3 ?mark)))
(<= (diag ?mark)
    (true (cell 1 3 ?mark))
    (true (cell 2 2 ?mark))
    (true (cell 3 1 ?mark)))

(<= (line ?mark)
    (index ?i)
    (row ?i ?mark))
(<= (line ?mark)
    (index ?j)
    (col ?j ?mark))
(<= (line ?mark)    
    (diag ?mark))
    
(<= (emptyCell ?i ?j)
    (index ?i)
    (index ?j)
    (true (cell ?i ?j b)))
(<= open    
    (emptyCell ?i ?j))