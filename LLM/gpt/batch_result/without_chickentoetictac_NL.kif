(role xplayer)
(role oplayer)

(location a)
(location b)
(location c)
(location d)
(location e)
(location f)
(location g)
(location h)
(location i)

(init (control xplayer))

(<= (legal xplayer noop)
    (true (control oplayer)))
(<= (legal xplayer (mark ?loc x))
    (true (control xplayer))
    (blank ?loc))
(<= (legal oplayer noop)
    (true (control xplayer)))
(<= (legal oplayer (mark ?loc o))
    (true (control oplayer))
    (blank ?loc))

(<= (next (cell ?loc ?mark))
    (role ?player)
    (does ?player (mark ?loc ?mark)))
(<= (next (cell ?loc ?mark))
    (true (cell ?loc ?mark)))
(<= (next (control xplayer))
    (true (control oplayer)))
(<= (next (control oplayer))
    (true (control xplayer)))

(<= terminal
    (line x))
(<= terminal
    (line o))
(<= terminal
    (not open))

(<= (goal xplayer 100)
    (line x))
(<= (goal xplayer 50)
    (not (line x))
    (not (line o))
    (not open))
(<= (goal xplayer 0)
    (line o))
(<= (goal xplayer 0)
    (not (line x))
    (not (line o))
    open)
(<= (goal oplayer 100)
    (line o))
(<= (goal oplayer 50)
    (not (line x))
    (not (line o))
    (not open))
(<= (goal oplayer 0)
    (line x))
(<= (goal oplayer 0)
    (not (line x))
    (not (line o))
    open)

(<= (row ?mark)
    (true (cell a ?mark))
    (true (cell b ?mark))
    (true (cell c ?mark)))
(<= (row ?mark)
    (true (cell d ?mark))
    (true (cell e ?mark))
    (true (cell f ?mark)))
(<= (row ?mark)
    (true (cell g ?mark))
    (true (cell h ?mark))
    (true (cell i ?mark)))
(<= (col ?mark)
    (true (cell a ?mark))
    (true (cell d ?mark))
    (true (cell g ?mark)))
(<= (col ?mark)
    (true (cell b ?mark))
    (true (cell e ?mark))
    (true (cell h ?mark)))
(<= (col ?mark)
    (true (cell c ?mark))
    (true (cell f ?mark))
    (true (cell i ?mark)))
(<= (diag ?mark)
    (true (cell a ?mark))
    (true (cell e ?mark))
    (true (cell i ?mark)))
(<= (diag ?mark)
    (true (cell c ?mark))
    (true (cell e ?mark))
    (true (cell g ?mark)))

(<= (line ?mark)
    (row ?mark))
(<= (line ?mark)
    (col ?mark))
(<= (line ?mark)
    (diag ?mark))

(<= (blank ?loc)
    (location ?loc)
    (not (true (cell ?loc x)))
    (not (true (cell ?loc o))))
(<= open
    (blank ?loc))