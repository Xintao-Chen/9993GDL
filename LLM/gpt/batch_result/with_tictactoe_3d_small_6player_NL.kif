(role xrow)
(role xcolumn)
(role xlevel)
(role orow)
(role ocolumn)
(role olevel)

(index 1)
(index 2)
(index 3)

(init (control xrow))

(<= (legal xrow noop)
    (true (control xcolumn)))
(<= (legal xrow noop)
    (true (control xlevel)))
(<= (legal xrow (select ?i ?j ?k))
    (true (control xrow))
    (index ?j)
    (index ?k)
    (emptyCell ?i ?j ?k))
(<= (legal xcolumn noop)
    (true (control xrow)))
(<= (legal xcolumn noop)
    (true (control xlevel)))
(<= (legal xcolumn (select ?i ?j ?k))
    (true (control xcolumn))
    (index ?i)
    (index ?k)
    (emptyCell ?i ?j ?k))
(<= (legal xlevel noop)
    (true (control xrow)))
(<= (legal xlevel noop)
    (true (control xcolumn)))
(<= (legal xlevel (select ?i ?j ?k))
    (true (control xlevel))
    (index ?i)
    (index ?j)
    (emptyCell ?i ?j ?k))
(<= (legal orow noop)
    (true (control ocolumn)))
(<= (legal orow noop)
    (true (control olevel)))
(<= (legal orow (select ?i ?j ?k))
    (true (control orow))
    (index ?j)
    (index ?k)
    (emptyCell ?i ?j ?k))
(<= (legal ocolumn noop)
    (true (control orow)))
(<= (legal ocolumn noop)
    (true (control olevel)))
(<= (legal ocolumn (select ?i ?j ?k))
    (true (control ocolumn))
    (index ?i)
    (index ?k)
    (emptyCell ?i ?j ?k))
(<= (legal olevel noop)
    (true (control orow)))
(<= (legal olevel noop)
    (true (control ocolumn)))
(<= (legal olevel (select ?i ?j ?k))
    (true (control olevel))
    (index ?i)
    (index ?j)
    (emptyCell ?i ?j ?k))

(<= (next (mark ?i ?j ?k x))
    (role ?player)
    (does ?player (select ?i ?j ?k))
    (true (control xrow)))
(<= (next (mark ?i ?j ?k o))
    (role ?player)
    (does ?player (select ?i ?j ?k))
    (true (control orow)))
(<= (next (mark ?i ?j ?k ?mark))
    (true (mark ?i ?j ?k ?mark)))

(<= (next (control xrow))
    (true (control olevel)))
(<= (next (control xcolumn))
    (true (control xrow)))
(<= (next (control xlevel))
    (true (control xcolumn)))
(<= (next (control orow))
    (true (control xlevel)))
(<= (next (control ocolumn))
    (true (control orow)))
(<= (next (control olevel))
    (true (control ocolumn)))

(<= terminal
    (line x))
(<= terminal
    (line o))
(<= terminal
    (not open))

(<= (goal xrow 100)
    (line x))
(<= (goal xcolumn 100)
    (line x))
(<= (goal xlevel 100)
    (line x))
(<= (goal orow 100)
    (line o))
(<= (goal ocolumn 100)
    (line o))
(<= (goal olevel 100)
    (line o))
(<= (goal xrow 50)
    (not (line x))
    (not (line o))
    (not open))
(<= (goal xcolumn 50)
    (not (line x))
    (not (line o))
    (not open))
(<= (goal xlevel 50)
    (not (line x))
    (not (line o))
    (not open))
(<= (goal orow 50)
    (not (line x))
    (not (line o))
    (not open))
(<= (goal ocolumn 50)
    (not (line x))
    (not (line o))
    (not open))
(<= (goal olevel 50)
    (not (line x))
    (not (line o))
    (not open))
(<= (goal xrow 0)
    (line o))
(<= (goal xcolumn 0)
    (line o))
(<= (goal xlevel 0)
    (line o))
(<= (goal orow 0)
    (line x))
(<= (goal ocolumn 0)
    (line x))
(<= (goal olevel 0)
    (line x))

(<= (row ?i ?j ?mark)
    (true (mark ?i ?j 1 ?mark))
    (true (mark ?i ?j 2 ?mark))
    (true (mark ?i ?j 3 ?mark)))
(<= (column ?i ?k ?mark)
    (true (mark ?i 1 ?k ?mark))
    (true (mark ?i 2 ?k ?mark))
    (true (mark ?i 3 ?k ?mark)))
(<= (level ?j ?k ?mark)
    (true (mark 1 ?j ?k ?mark))
    (true (mark 2 ?j ?k ?mark))
    (true (mark 3 ?j ?k ?mark)))
(<= (diag1 ?mark)
    (true (mark 1 1 1 ?mark))
    (true (mark 2 2 2 ?mark))
    (true (mark 3 3 3 ?mark)))
(<= (diag2 ?mark)
    (true (mark 1 1 3 ?mark))
    (true (mark 2 2 2 ?mark))
    (true (mark 3 3 1 ?mark)))
(<= (diag3 ?mark)
    (true (mark 1 3 1 ?mark))
    (true (mark 2 2 2 ?mark))
    (true (mark 3 1 3 ?mark)))
(<= (diag4 ?mark)
    (true (mark 3 1 1 ?mark))
    (true (mark 2 2 2 ?mark))
    (true (mark 1 3 3 ?mark)))

(<= (line ?mark)
    (index ?i)
    (index ?j)
    (row ?i ?j ?mark))
(<= (line ?mark)
    (index ?i)
    (index ?k)
    (column ?i ?k ?mark))
(<= (line ?mark)
    (index ?j)
    (index ?k)
    (level ?j ?k ?mark))
(<= (line ?mark)
    (diag1 ?mark))
(<= (line ?mark)
    (diag2 ?mark))
(<= (line ?mark)
    (diag3 ?mark))
(<= (line ?mark)
    (diag4 ?mark))

(<= (emptyCell ?i ?j ?k)
    (index ?i)
    (index ?j)
    (index ?k)
    (not (true (mark ?i ?j ?k x)))
    (not (true (mark ?i ?j ?k o))))
(<= open
    (emptyCell ?i ?j ?k))