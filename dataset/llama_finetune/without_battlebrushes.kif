(role blue)
(role red)
(role green)
(role yellow)
(startingcell 2 2)
(startingcell 2 7)
(startingcell 7 2)
(startingcell 7 7)
(<= (init (cell ?x ?y b))
    (coordinate ?x)
    (coordinate ?y)
    (not (startingcell ?x ?y)))
(init (cell 2 2 blue))
(init (cell 2 7 red))
(init (cell 7 2 green))
(init (cell 7 7 yellow))
(init (at bluebrush 2 2))
(init (at redbrush 2 7))
(init (at greenbrush 7 2))
(init (at yellowbrush 7 7))
(brush blue bluebrush)
(brush red redbrush)
(brush green greenbrush)
(brush yellow yellowbrush)
(<= (next (cell ?x ?y ?c))
    (nextcellcolor ?x ?y ?c))
(<= (nextcellcolor ?x ?y ?s)
    (true (cell ?x ?y ?s))
    (not (does blue (goto ?x ?y)))
    (not (does red (goto ?x ?y)))
    (not (does green (goto ?x ?y)))
    (not (does yellow (goto ?x ?y))))
(<= (nextcellcolor ?x ?y ?s)
    (true (cell ?x ?y ?s))
    (role ?r)
    (does ?r (goto ?x ?y))
    (role ?r2)
    (distinct ?r ?r2)
    (does ?r2 (goto ?x ?y)))
(<= (nextcellcolor ?x ?y blue)
    (does blue (goto ?x ?y))
    (not (does red (goto ?x ?y)))
    (not (does green (goto ?x ?y)))
    (not (does yellow (goto ?x ?y))))
(<= (nextcellcolor ?x ?y red)
    (does red (goto ?x ?y))
    (not (does blue (goto ?x ?y)))
    (not (does green (goto ?x ?y)))
    (not (does yellow (goto ?x ?y))))
(<= (nextcellcolor ?x ?y green)
    (does green (goto ?x ?y))
    (not (does blue (goto ?x ?y)))
    (not (does red (goto ?x ?y)))
    (not (does yellow (goto ?x ?y))))
(<= (nextcellcolor ?x ?y yellow)
    (does yellow (goto ?x ?y))
    (not (does blue (goto ?x ?y)))
    (not (does red (goto ?x ?y)))
    (not (does green (goto ?x ?y))))
(<= (legal ?r (goto ?x ?y))
    (role ?r)
    (not (true (dizzy ?r 1)))
    (not (true (dizzy ?r 0)))
    (true (cell ?x ?y ?s))
    (not (occupied ?x ?y))
    (brush ?r ?b)
    (true (at ?b ?i ?j))
    (adjacent ?x ?y ?i ?j))
(<= (occupied ?x ?y)
    (true (at ?b ?x ?y)))
(<= (legal ?r feeldizzy)
    (true (dizzy ?r ?n)))
(<= (legal ?r feeldizzy)
    (brush ?r ?b)
    (true (at ?b ?x ?y))
    (not (hasAdjacentEmptyCell ?x ?y)))
(<= (hasAdjacentEmptyCell ?x ?y)
    (adjacent ?x ?y ?x2 ?y2)
    (not (occupied ?x2 ?y2)))
(<= (adjacent ?x ?y ?x ?j)
    (coordinate ?x)
    (nextIndex ?y ?j))
(<= (adjacent ?x ?y ?x ?j)
    (coordinate ?x)
    (nextIndex ?j ?y))
(<= (adjacent ?x ?y ?i ?y)
    (coordinate ?y)
    (nextIndex ?x ?i))
(<= (adjacent ?x ?y ?i ?y)
    (coordinate ?y)
    (nextIndex ?i ?x))
(<= (next (at bluebrush ?x ?y))
    (does blue (goto ?x ?y))
    (not (does red (goto ?x ?y)))
    (not (does green (goto ?x ?y)))
    (not (does yellow (goto ?x ?y))))
(<= (next (at redbrush ?x ?y))
    (does red (goto ?x ?y))
    (not (does blue (goto ?x ?y)))
    (not (does green (goto ?x ?y)))
    (not (does yellow (goto ?x ?y))))
(<= (next (at greenbrush ?x ?y))
    (does green (goto ?x ?y))
    (not (does blue (goto ?x ?y)))
    (not (does red (goto ?x ?y)))
    (not (does yellow (goto ?x ?y))))
(<= (next (at yellowbrush ?x ?y))
    (does yellow (goto ?x ?y))
    (not (does blue (goto ?x ?y)))
    (not (does red (goto ?x ?y)))
    (not (does green (goto ?x ?y))))
(<= (next (at ?b ?x ?y))
    (role ?r)
    (brush ?r ?b)
    (true (at ?b ?x ?y))
    (willfeeldizzy ?r))
(<= (next (at ?b ?x ?y))
    (role ?r)
    (brush ?r ?b)
    (true (at ?b ?x ?y))
    (does ?r feeldizzy))
(<= (next (dizzy ?r 1))
    (willfeeldizzy ?r))
(<= (next (dizzy ?r 0))
    (true (dizzy ?r 1)))
(<= (willfeeldizzy ?r)
    (role ?r)
    (does ?r (goto ?i ?j))
    (role ?r2)
    (distinct ?r ?r2)
    (does ?r2 (goto ?i ?j)))
(nextIndex 1 2)
(nextIndex 2 3)
(nextIndex 3 4)
(nextIndex 4 5)
(nextIndex 5 6)
(nextIndex 6 7)
(nextIndex 7 8)
(<= (threeDistinctRoles ?r1 ?r2 ?r3)
    (role ?r1)
    (role ?r2)
    (role ?r3)
    (distinct ?r1 ?r2)
    (distinct ?r1 ?r3)
    (distinct ?r2 ?r3))
(succ -1 0)
(succ 0 1)
(<= (moreThan ?n ?n2)
    (succ ?n2 ?n))
(<= (moreThan ?n ?n2)
    (succ ?t ?n)
    (moreThan ?t ?n2))
(succ 1 2)
(succ 2 3)
(succ 3 4)
(succ 4 5)
(succ 5 6)
(succ 6 7)
(succ 7 8)
(succ 8 9)
(succ 9 10)
(succ 10 11)
(succ 11 12)
(succ 12 13)
(succ 13 14)
(succ 14 15)
(succ 15 16)
(succ 16 17)
(succ 17 18)
(succ 18 19)
(succ 19 20)
(<= (points blue ?p)
    (calculatePoints blue ?p 1 1))
(<= (points red ?p)
    (calculatePoints red ?p 1 1))
(<= (points green ?p)
    (calculatePoints green ?p 1 1))
(<= (points yellow ?p)
    (calculatePoints yellow ?p 1 1))
(<= (calculatePoints ?r ?pp1 ?x ?y)
    (true (cell ?x ?y ?r))
    (nextIndex ?y ?y2)
    (calculatePoints ?r ?p ?x ?y2)
    (succ ?p ?pp1))
(<= (calculatePoints ?r ?pp1 ?x 8)
    (true (cell ?x 8 ?r))
    (nextIndex ?x ?x2)
    (calculatePoints ?r ?p ?x2 1)
    (succ ?p ?pp1))
(<= (calculatePoints ?r ?p ?x ?y)
    (coordinate ?x)
    (coordinate ?y)
    (role ?r)
    (not (true (cell ?x ?y ?r)))
    (nextIndex ?y ?y2)
    (calculatePoints ?r ?p ?x ?y2))
(<= (calculatePoints ?r ?p ?x 8)
    (coordinate ?x)
    (role ?r)
    (not (true (cell ?x 8 ?r)))
    (nextIndex ?x ?x2)
    (calculatePoints ?r ?p ?x2 1))
(<= (calculatePoints ?r 1 8 8)
    (true (cell 8 8 ?r)))
(<= (calculatePoints ?r 0 8 8)
    (role ?r)
    (not (true (cell 8 8 ?r))))
(init (step 1))
(<= (next (step ?n2))
    (true (step ?n))
    (succ ?n ?n2))
(<= terminal
    (true (step 20)))
(coordinate 1)
(coordinate 2)
(coordinate 3)
(coordinate 4)
(coordinate 5)
(coordinate 6)
(coordinate 7)
(coordinate 8)
(<= (sameScore ?r1 ?r2)
    (points ?r1 ?n)
    (points ?r2 ?n)
    (distinct ?r1 ?r2))
(<= (higherScore ?r1 ?r2)
    (points ?r1 ?n1)
    (points ?r2 ?n2)
    (moreThan ?n1 ?n2))
(<= (goal ?r 100)
    (true (step 20))
    (higherScore ?r ?r2)
    (higherScore ?r ?r3)
    (higherScore ?r ?r4)
    (threeDistinctRoles ?r2 ?r3 ?r4))
(<= (goal ?r 50)
    (true (step 20))
    (higherScore ?r2 ?r)
    (higherScore ?r ?r3)
    (higherScore ?r ?r4)
    (distinct ?r3 ?r4))
(<= (goal ?r 25)
    (true (step 20))
    (higherScore ?r2 ?r)
    (higherScore ?r3 ?r)
    (distinct ?r2 ?r3)
    (higherScore ?r ?r4))
(<= (goal ?r 0)
    (true (step 20))
    (higherScore ?r2 ?r)
    (higherScore ?r3 ?r)
    (higherScore ?r4 ?r)
    (threeDistinctRoles ?r2 ?r3 ?r4))
(<= (goal ?r 75)
    (true (step 20))
    (sameScore ?r ?r2)
    (higherScore ?r ?r3)
    (higherScore ?r ?r4)
    (distinct ?r3 ?r4))
(<= (goal ?r 66)
    (true (step 20))
    (sameScore ?r ?r2)
    (sameScore ?r ?r3)
    (distinct ?r2 ?r3)
    (higherScore ?r ?r4))
(<= (goal ?r 50)
    (true (step 20))
    (sameScore ?r ?r2)
    (sameScore ?r ?r3)
    (sameScore ?r ?r4)
    (threeDistinctRoles ?r2 ?r3 ?r4))
(<= (goal ?r 33)
    (true (step 20))
    (sameScore ?r ?r2)
    (higherScore ?r3 ?r)
    (higherScore ?r ?r4))
(<= (goal ?r 25)
    (true (step 20))
    (sameScore ?r ?r2)
    (sameScore ?r ?r3)
    (distinct ?r2 ?r3)
    (higherScore ?r4 ?r))
(<= (goal ?r 12)
    (true (step 20))
    (sameScore ?r ?r2)
    (higherScore ?r3 ?r)
    (higherScore ?r4 ?r)
    (distinct ?r3 ?r4))
(<= (goal ?r 0)
    (role ?r)
    (not (true (step 20))))
