; Shogi (Capture the King)
;
; GDL by Alex Landau
;
; Like the "Speed Chess" and "Capture the King" variants of chess, this
; is a variant of Shogi in which direct capture of the king replaces
; checkmate as a win condition. Accordingly, the concept of check has
; been removed, which simplifies the game rules.
;
; This does change one other rule of the game, "Uchifuzume": the restriction
; that a pawn cannot be dropped to cause checkmate. Without computing check,
; this cannot be determined, so we drop the restriction instead.
;
; In addition to these changes, this version uses a step counter and
; eliminates some other possible causes of stalemate, as well as the
; impasse rule.

(role black)
(role white)

(init (control black))
(init (step 1))

(<= (init (inHand ?role ?piece 0))
    (role ?role)
    (droppablePieceType ?piece))

(init (cell 1 a white lance unpromoted))
(init (cell 2 a white knight unpromoted))
(init (cell 3 a white silver unpromoted))
(init (cell 4 a white gold unpromotable))
(init (cell 5 a white king unpromotable))
(init (cell 6 a white gold unpromotable))
(init (cell 7 a white silver unpromoted))
(init (cell 8 a white knight unpromoted))
(init (cell 9 a white lance unpromoted))
(init (cell 2 b white bishop unpromoted))
(init (cell 8 b white rook unpromoted))
(init (cell 1 c white pawn unpromoted))
(init (cell 2 c white pawn unpromoted))
(init (cell 3 c white pawn unpromoted))
(init (cell 4 c white pawn unpromoted))
(init (cell 5 c white pawn unpromoted))
(init (cell 6 c white pawn unpromoted))
(init (cell 7 c white pawn unpromoted))
(init (cell 8 c white pawn unpromoted))
(init (cell 9 c white pawn unpromoted))

(init (cell 1 i black lance unpromoted))
(init (cell 2 i black knight unpromoted))
(init (cell 3 i black silver unpromoted))
(init (cell 4 i black gold unpromotable))
(init (cell 5 i black king unpromotable))
(init (cell 6 i black gold unpromotable))
(init (cell 7 i black silver unpromoted))
(init (cell 8 i black knight unpromoted))
(init (cell 9 i black lance unpromoted))
(init (cell 2 h black rook unpromoted))
(init (cell 8 h black bishop unpromoted))
(init (cell 1 g black pawn unpromoted))
(init (cell 2 g black pawn unpromoted))
(init (cell 3 g black pawn unpromoted))
(init (cell 4 g black pawn unpromoted))
(init (cell 5 g black pawn unpromoted))
(init (cell 6 g black pawn unpromoted))
(init (cell 7 g black pawn unpromoted))
(init (cell 8 g black pawn unpromoted))
(init (cell 9 g black pawn unpromoted))

; Legal moves

(<= (legal ?role noop)
    (role ?role)
    (not (true (control ?role))))

; Moves except for lance, bishop, and knight moves and drops
; Figure out which moves are available before figuring out promotion...
(<= (legalMove ?role ?x1 ?y1 ?x2 ?y2)
    (true (cell ?x1 ?y1 ?role ?piece ?promotionStatus))
    (moveType ?piece ?promotionStatus ?moveType)
    (legalDestination ?role ?moveType ?x1 ?y1 ?x2 ?y2)
    (not (occupiedBy ?x2 ?y2 ?role)))

(<= (legalMove ?role ?x1 ?y1 ?x2 ?y2)
    (legalLongMove ?role ?x1 ?y1 ?x2 ?y2 ?dir))
(<= (legalLongMove ?role ?x1 ?y1 ?x2 ?y2 ?dir)
    (true (cell ?x1 ?y1 ?role ?piece ?promotionStatus))
    (longMoveDir ?piece ?promotionStatus ?role ?dir)
    (oneInDir ?x1 ?y1 ?x2 ?y2 ?dir)
    (not (occupiedBy ?x2 ?y2 ?role)))
(<= (legalLongMove ?role ?x1 ?y1 ?x3 ?y3 ?dir)
    (legalLongMove ?role ?x1 ?y1 ?x2 ?y2 ?dir)
    (empty ?x2 ?y2)
    (oneInDir ?x2 ?y2 ?x3 ?y3 ?dir)
    (not (occupiedBy ?x3 ?y3 ?role)))

(<= (longMoveDir bishop unpromoted ?role ?dir)
    (role ?role)
    (diagonal ?dir))
(<= (longMoveDir bishop promoted ?role ?dir)
    (role ?role)
    (diagonal ?dir))
(<= (longMoveDir rook unpromoted ?role ?dir)
    (role ?role)
    (straight ?dir))
(<= (longMoveDir rook promoted ?role ?dir)
    (role ?role)
    (straight ?dir))
(<= (longMoveDir lance unpromoted ?role ?dir)
    (forwardOrthogonal ?role ?dir))

; Figure out promotion for the legal moves
(<= (legal ?role (move ?x1 ?y1 ?x2 ?y2))
    (true (control ?role))
    (legalMove ?role ?x1 ?y1 ?x2 ?y2)
    (or (true (cell ?x1 ?y1 ?role ?piece promoted))
        (true (cell ?x1 ?y1 ?role ?piece unpromotable))))
(<= (legal ?role (move ?x1 ?y1 ?x2 ?y2))
    (true (control ?role))
    (legalMove ?role ?x1 ?y1 ?x2 ?y2)
    (true (cell ?x1 ?y1 ?role ?piece unpromoted))
    (not (forcedPromotionRank ?piece ?role ?y2)))
(<= (legal ?role (promote ?x1 ?y1 ?x2 ?y2))
    (true (control ?role))
    (legalMove ?role ?x1 ?y1 ?x2 ?y2)
    (true (cell ?x1 ?y1 ?role ?piece unpromoted))
    (or (promotionRank ?role ?y2)
        (promotionRank ?role ?y1)))

(<= (legal ?role (drop ?piece ?x ?y))
    (true (control ?role))
    (droppablePiece ?role ?piece ?x ?y))
(<= (droppablePiece ?role ?piece ?x ?y)
    (true (inHand ?role ?piece ?n))
    (distinct ?n 0)
    (distinct ?piece pawn)
    (not (forcedPromotionRank ?piece ?role ?y))
    (empty ?x ?y))
; Pawns can't be dropped if the player has an unpromoted pawn in that file already
(<= (droppablePiece ?role pawn ?x ?y)
    (true (inHand ?role pawn ?n))
    (distinct ?n 0)
    (not (forcedPromotionRank pawn ?role ?y))
    (empty ?x ?y)
    (not (unpromotedPawnInFile ?role ?x)))

(<= (unpromotedPawnInFile ?role ?x)
    (true (cell ?x ?y ?role pawn unpromoted)))

(<= (empty ?x ?y)
    (file ?x)
    (rank ?y)
    (not (occupied ?x ?y)))
(<= (occupied ?x ?y)
    (true (cell ?x ?y ?anyRole ?anyPiece ?anyPromotionStatus)))
(<= (occupiedBy ?x ?y ?role)
    (true (cell ?x ?y ?role ?anyPiece ?anyPromotionStatus)))

; Game dynamics

(<= (next (control white))
    (true (control black)))
(<= (next (control black))
    (true (control white)))
(<= (next (step ?np1))
    (true (step ?n))
    (succ ?n ?np1))

(<= (next (cell ?x ?y ?role ?piece ?promotionStatus))
    (true (cell ?x ?y ?role ?piece ?promotionStatus))
    (not (affected ?x ?y)))
(<= (next (cell ?x2 ?y2 ?role ?piece ?promotionStatus))
    (does ?role (move ?x1 ?y1 ?x2 ?y2))
    (true (cell ?x1 ?y1 ?role ?piece ?promotionStatus)))
(<= (next (cell ?x2 ?y2 ?role ?piece promoted))
    (does ?role (promote ?x1 ?y1 ?x2 ?y2))
    (true (cell ?x1 ?y1 ?role ?piece unpromoted)))
(<= (next (cell ?x ?y ?role ?piece ?promotionStatus))
    (does ?role (drop ?piece ?x ?y))
    (defaultPromotionStatus ?piece ?promotionStatus))

; Put captured pieces in hand
(<= (capturedPieceOfType ?piece)
    (or (does ?role (move ?x1 ?y1 ?x2 ?y2))
        (does ?role (promote ?x1 ?y1 ?x2 ?y2)))
    (true (cell ?x2 ?y2 ?opponent ?piece ?promotionStatus)))
(<= (droppingPieceOfType ?piece)
    (does ?role (drop ?piece ?x ?y)))
(<= (next (inHand ?role ?piece ?n))
    (true (inHand ?role ?piece ?n))
    (not (true (control ?role))))
(<= (next (inHand ?role ?piece ?n))
    (true (inHand ?role ?piece ?n))
    (true (control ?role))
    (not (capturedPieceOfType ?piece))
    (not (droppingPieceOfType ?piece)))
(<= (next (inHand ?role ?piece ?np1))
    (true (inHand ?role ?piece ?n))
    (true (control ?role))
    (capturedPieceOfType ?piece)
    (not (droppingPieceOfType ?piece))
    (succ ?n ?np1))
(<= (next (inHand ?role ?piece ?n))
    (true (inHand ?role ?piece ?n))
    (true (control ?role))
    (capturedPieceOfType ?piece)
    (droppingPieceOfType ?piece))
(<= (next (inHand ?role ?piece ?nm1))
    (true (inHand ?role ?piece ?n))
    (true (control ?role))
    (not (capturedPieceOfType ?piece))
    (droppingPieceOfType ?piece)
    (succ ?nm1 ?n))


(<= (affected ?x1 ?y1)
    (does ?role (move ?x1 ?y1 ?x2 ?y2)))
(<= (affected ?x2 ?y2)
    (does ?role (move ?x1 ?y1 ?x2 ?y2)))
(<= (affected ?x1 ?y1)
    (does ?role (promote ?x1 ?y1 ?x2 ?y2)))
(<= (affected ?x2 ?y2)
    (does ?role (promote ?x1 ?y1 ?x2 ?y2)))
; We don't need to include this for drop, because 


(defaultPromotionStatus king unpromotable)
(defaultPromotionStatus gold unpromotable)
(defaultPromotionStatus silver unpromoted)
(defaultPromotionStatus knight unpromoted)
(defaultPromotionStatus lance unpromoted)
(defaultPromotionStatus rook unpromoted)
(defaultPromotionStatus bishop unpromoted)
(defaultPromotionStatus pawn unpromoted)



; Terminality and goals

(<= (goal ?role 0)
    (lost ?role))
(<= (goal ?role 50)
    (opponent ?role ?opp)
    (not (lost ?role))
    (not (lost ?opp)))
(<= (goal ?role 100)
    (opponent ?role ?opp)
    (lost ?opp))

(<= (lost ?role)
    (role ?role)
    (not (hasKing ?role)))
; An inability to move (including no available drops) is considered a loss for the player unable to move.
(<= (lost ?role)
    (noLegalMovesFor ?role))

(<= (hasKing ?role)
    (true (cell ?x ?y ?role king unpromotable)))
(<= (noLegalMovesFor ?role)
    (true (control ?role))
    (not (anyLegalMoveFor ?role))
    (not (anyDroppablePieceFor ?role))) 
(<= (anyLegalMoveFor ?role)
    (legalMove ?role ?x1 ?y1 ?x2 ?y2))
(<= (anyDroppablePieceFor ?role)
    (droppablePiece ?role ?piece ?x ?y))

(<= terminal
    (lost ?role))
(<= terminal
    (true (step 201)))

; Constants

(opponent black white)
(opponent white black)

(moveType king unpromotable king)
(moveType gold unpromotable gold)
(moveType silver unpromoted silver)
(moveType silver promoted gold)
(moveType knight unpromoted knight)
(moveType knight promoted gold)
(moveType lance promoted gold)
(moveType bishop promoted king)
(moveType rook promoted king)
(moveType pawn unpromoted pawn)
(moveType pawn promoted gold)

(promotablePieceType silver)
(promotablePieceType knight)
(promotablePieceType lance)
(promotablePieceType bishop)
(promotablePieceType rook)
(promotablePieceType pawn)

(forcedPromotionRank pawn black a)
(forcedPromotionRank lance black a)
(forcedPromotionRank knight black a)
(forcedPromotionRank knight black b)
(forcedPromotionRank pawn white i)
(forcedPromotionRank lance white i)
(forcedPromotionRank knight white i)
(forcedPromotionRank knight white h)
(promotionRank black a)
(promotionRank black b)
(promotionRank black c)
(promotionRank white g)
(promotionRank white h)
(promotionRank white i)

(droppablePieceType gold)
(droppablePieceType silver)
(droppablePieceType knight)
(droppablePieceType lance)
(droppablePieceType bishop)
(droppablePieceType rook)
(droppablePieceType pawn)

; Note that the first argument to legalDestination is a "move type" that does not necessarily
; correspond directly to a piece type.
; Also, these are not intended to cover the cases of rook, bishop, or lance moves that cover
; multiple spaces. It does, however, cover knight moves.
(<= (legalDestination ?role orthogonal ?x1 ?y1 ?x2 ?y2)
    (role ?role)
    (straight ?dir)
    (oneInDir ?x1 ?y1 ?x2 ?y2 ?dir))
(<= (legalDestination ?role diagonal ?x1 ?y1 ?x2 ?y2)
    (role ?role)
    (diagonal ?dir)
    (oneInDir ?x1 ?y1 ?x2 ?y2 ?dir))

; Kings: One in any direction
(<= (legalDestination ?role king ?x1 ?y1 ?x2 ?y2)
    (role ?role)
    (dir ?dir)
    (oneInDir ?x1 ?y1 ?x2 ?y2 ?dir))

; Gold general: One orthogonally, or forward diagonally
(<= (legalDestination ?role gold ?x1 ?y1 ?x2 ?y2)
    (legalDestination ?role orthogonal ?x1 ?y1 ?x2 ?y2))
(<= (legalDestination ?role gold ?x1 ?y1 ?x2 ?y2)
    (forwardDiagonal ?role ?dir)
    (oneInDir ?x1 ?y1 ?x2 ?y2 ?dir))

; Silver general: One diagonally, or like a pawn
(<= (legalDestination ?role gold ?x1 ?y1 ?x2 ?y2)
    (legalDestination ?role diagonal ?x1 ?y1 ?x2 ?y2))
(<= (legalDestination ?role gold ?x1 ?y1 ?x2 ?y2)
    (legalDestination ?role pawn ?x1 ?y1 ?x2 ?y2))

; Knight: Two forward, plus once to the side
(<= (legalDestination ?role knight ?x1 ?y1 ?x3 ?y3)
    (forwardOrthogonal ?role ?forwardDir)
    (oneInDir ?x1 ?y1 ?x2 ?y2 ?forwardDir)
    (forwardDiagonal ?role ?diagonalDir)
    (oneInDir ?x2 ?y2 ?x3 ?y3 ?diagonalDir))

; Pawn: One forward
(<= (legalDestination ?role pawn ?x1 ?y1 ?x2 ?y2)
    (forwardOrthogonal ?role ?dir)
    (oneInDir ?x1 ?y1 ?x2 ?y2 ?dir))



(dir n)
(dir s)
(dir e)
(dir w)
(dir nw)
(dir ne)
(dir se)
(dir sw)
(straight n)
(straight s)
(straight e)
(straight w)
(diagonal nw)
(diagonal ne)
(diagonal se)
(diagonal sw)
(forwardDiagonal black sw)
(forwardDiagonal black se)
(forwardDiagonal white nw)
(forwardDiagonal white ne)
(forwardOrthogonal black s)
(forwardOrthogonal white n)

(file 1)
(file 2)
(file 3)
(file 4)
(file 5)
(file 6)
(file 7)
(file 8)
(file 9)
(rank a)
(rank b)
(rank c)
(rank d)
(rank e)
(rank f)
(rank g)
(rank h)
(rank i)

(nextFile 1 2)
(nextFile 2 3)
(nextFile 3 4)
(nextFile 4 5)
(nextFile 5 6)
(nextFile 6 7)
(nextFile 7 8)
(nextFile 8 9)
(nextRank a b)
(nextRank b c)
(nextRank c d)
(nextRank d e)
(nextRank e f)
(nextRank f g)
(nextRank g h)
(nextRank h i)

; north goes from a to b, i.e. in the direction white advances
(<= (oneInDir ?x ?y1 ?x ?y2 n)
    (file ?x)
    (nextRank ?y1 ?y2))
(<= (oneInDir ?x1 ?y1 ?x2 ?y2 ne)
    (nextFile ?x1 ?x2)
    (nextRank ?y1 ?y2))
(<= (oneInDir ?x1 ?y ?x2 ?y e)
    (nextFile ?x1 ?x2)
    (rank ?y))
(<= (oneInDir ?x1 ?y2 ?x2 ?y1 se)
    (nextFile ?x1 ?x2)
    (nextRank ?y1 ?y2))
(<= (oneInDir ?x ?y2 ?x ?y1 s)
    (file ?x)
    (nextRank ?y1 ?y2))
(<= (oneInDir ?x2 ?y2 ?x1 ?y1 sw)
    (nextFile ?x1 ?x2)
    (nextRank ?y1 ?y2))
(<= (oneInDir ?x2 ?y ?x1 ?y w)
    (nextFile ?x1 ?x2)
    (rank ?y))
(<= (oneInDir ?x2 ?y1 ?x1 ?y2 nw)
    (nextFile ?x1 ?x2)
    (nextRank ?y1 ?y2))

(<= (lt ?m ?n)
    (succ ?m ?n))
(<= (lt ?l ?n)
    (succ ?m ?n)
    (lt ?l ?m))

; Successor function for the step counter and in-hand counts
(succ 0 1)
(succ 1 2)
(succ 2 3)
(succ 3 4)
(succ 4 5)
(succ 5 6)
(succ 6 7)
(succ 7 8)
(succ 8 9)
(succ 9 10)
(succ 10 11)
(succ 11 12)
(succ 12 13)
(succ 13 14)
(succ 14 15)
(succ 15 16)
(succ 16 17)
(succ 17 18)
(succ 18 19)
(succ 19 20)
(succ 20 21)
(succ 21 22)
(succ 22 23)
(succ 23 24)
(succ 24 25)
(succ 25 26)
(succ 26 27)
(succ 27 28)
(succ 28 29)
(succ 29 30)
(succ 30 31)
(succ 31 32)
(succ 32 33)
(succ 33 34)
(succ 34 35)
(succ 35 36)
(succ 36 37)
(succ 37 38)
(succ 38 39)
(succ 39 40)
(succ 40 41)
(succ 41 42)
(succ 42 43)
(succ 43 44)
(succ 44 45)
(succ 45 46)
(succ 46 47)
(succ 47 48)
(succ 48 49)
(succ 49 50)
(succ 50 51)
(succ 51 52)
(succ 52 53)
(succ 53 54)
(succ 54 55)
(succ 55 56)
(succ 56 57)
(succ 57 58)
(succ 58 59)
(succ 59 60)
(succ 60 61)
(succ 61 62)
(succ 62 63)
(succ 63 64)
(succ 64 65)
(succ 65 66)
(succ 66 67)
(succ 67 68)
(succ 68 69)
(succ 69 70)
(succ 70 71)
(succ 71 72)
(succ 72 73)
(succ 73 74)
(succ 74 75)
(succ 75 76)
(succ 76 77)
(succ 77 78)
(succ 78 79)
(succ 79 80)
(succ 80 81)
(succ 81 82)
(succ 82 83)
(succ 83 84)
(succ 84 85)
(succ 85 86)
(succ 86 87)
(succ 87 88)
(succ 88 89)
(succ 89 90)
(succ 90 91)
(succ 91 92)
(succ 92 93)
(succ 93 94)
(succ 94 95)
(succ 95 96)
(succ 96 97)
(succ 97 98)
(succ 98 99)
(succ 99 100)
(succ 100 101)
(succ 101 102)
(succ 102 103)
(succ 103 104)
(succ 104 105)
(succ 105 106)
(succ 106 107)
(succ 107 108)
(succ 108 109)
(succ 109 110)
(succ 110 111)
(succ 111 112)
(succ 112 113)
(succ 113 114)
(succ 114 115)
(succ 115 116)
(succ 116 117)
(succ 117 118)
(succ 118 119)
(succ 119 120)
(succ 120 121)
(succ 121 122)
(succ 122 123)
(succ 123 124)
(succ 124 125)
(succ 125 126)
(succ 126 127)
(succ 127 128)
(succ 128 129)
(succ 129 130)
(succ 130 131)
(succ 131 132)
(succ 132 133)
(succ 133 134)
(succ 134 135)
(succ 135 136)
(succ 136 137)
(succ 137 138)
(succ 138 139)
(succ 139 140)
(succ 140 141)
(succ 141 142)
(succ 142 143)
(succ 143 144)
(succ 144 145)
(succ 145 146)
(succ 146 147)
(succ 147 148)
(succ 148 149)
(succ 149 150)
(succ 150 151)
(succ 151 152)
(succ 152 153)
(succ 153 154)
(succ 154 155)
(succ 155 156)
(succ 156 157)
(succ 157 158)
(succ 158 159)
(succ 159 160)
(succ 160 161)
(succ 161 162)
(succ 162 163)
(succ 163 164)
(succ 164 165)
(succ 165 166)
(succ 166 167)
(succ 167 168)
(succ 168 169)
(succ 169 170)
(succ 170 171)
(succ 171 172)
(succ 172 173)
(succ 173 174)
(succ 174 175)
(succ 175 176)
(succ 176 177)
(succ 177 178)
(succ 178 179)
(succ 179 180)
(succ 180 181)
(succ 181 182)
(succ 182 183)
(succ 183 184)
(succ 184 185)
(succ 185 186)
(succ 186 187)
(succ 187 188)
(succ 188 189)
(succ 189 190)
(succ 190 191)
(succ 191 192)
(succ 192 193)
(succ 193 194)
(succ 194 195)
(succ 195 196)
(succ 196 197)
(succ 197 198)
(succ 198 199)
(succ 199 200)
(succ 200 201)

; Bases and inputs

(base (control black))
(base (control white))
(<= (base (step ?n))
    (succ ?m ?n))
(<= (base (inHand ?role ?piece ?n))
    (role ?role)
    (droppablePieceType ?piece)
    (distinct ?piece bishop)
    (distinct ?piece rook)
    (lt ?n 5))
(<= (base (inHand ?role pawn ?n))
    (role ?role)
    (lt ?n 19))
(<= (base (inHand ?role bishop ?n))
    (role ?role)
    (lt ?n 3))
(<= (base (inHand ?role rook ?n))
    (role ?role)
    (lt ?n 3))

(<= (base (cell ?x ?y ?role king unpromotable))
    (file ?x)
    (rank ?y)
    (role ?role))
(<= (base (cell ?x ?y ?role gold unpromotable))
    (file ?x)
    (rank ?y)
    (role ?role))
(<= (base (cell ?x ?y ?role ?piece unpromoted))
    (file ?x)
    (rank ?y)
    (role ?role)
    (promotablePieceType ?piece)
    (not (forcedPromotionRank ?piece ?role ?y)))
(<= (base (cell ?x ?y ?role ?piece promoted))
    (file ?x)
    (rank ?y)
    (role ?role)
    (promotablePieceType ?piece))

(input black noop)
(input white noop)
(<= (input ?role (move ?x1 ?y1 ?x2 ?y2))
    (role ?role)
    (oneInDir ?x1 ?y1 ?x2 ?y2 ?anyDir))
(<= (input ?role (move ?x1 ?y1 ?x2 ?y2))
    (role ?role)
    (inSameLine ?x1 ?y1 ?x2 ?y2 ?anyDir))
(<= (input ?role (move ?x1 ?y1 ?x2 ?y2))
    (legalDestination ?role knight ?x1 ?y1 ?x2 ?y2))
(<= (input ?role (promote ?x1 ?y1 ?x2 ?y2))
    (role ?role)
    (oneInDir ?x1 ?y1 ?x2 ?y2 ?anyDir)
    (or (promotionRank ?role ?y1)
        (promotionRank ?role ?y2)))
(<= (input ?role (promote ?x1 ?y1 ?x2 ?y2))
    (role ?role)
    (inSameLine ?x1 ?y1 ?x2 ?y2 ?anyDir)
    (or (promotionRank ?role ?y1)
        (promotionRank ?role ?y2)))
(<= (input ?role (promote ?x1 ?y1 ?x2 ?y2))
    (legalDestination ?role knight ?x1 ?y1 ?x2 ?y2)
    (promotionRank ?role ?y2))
(<= (input ?role (drop ?piece ?x ?y))
    (role ?role)
    (droppablePieceType ?piece)
    (file ?x)
    (rank ?y)
    (not (forcedPromotionRank ?piece ?role ?y)))

(<= (inSameLine ?x1 ?y1 ?x2 ?y2 ?dir)
    (oneInDir ?x1 ?y1 ?x2 ?y2 ?dir))
(<= (inSameLine ?x1 ?y1 ?x3 ?y3 ?dir)
    (oneInDir ?x1 ?y1 ?x2 ?y2 ?dir)
    (inSameLine ?x2 ?y2 ?x3 ?y3 ?dir))
